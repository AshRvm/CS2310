CHIP CLA84  {
	//AS: add/subtract line (1: subtract), co: carry-out, of: overflow
	IN		a[8], b[8], us, AS;
	OUT		s[8], co, of;

	PARTS:
	//p[0-9]+ : Pi
	//P([0-9]+)b([0-9]+) : P[\1..\2]

	//extra step: Invert b-input if AS is active
	Xor4b1(a=b[0..3],   b=AS, out=b0b3);
	Xor4b1(a=b[4..7],   b=AS, out=b4b7);

	//////////    t = 1tau: Pi, Gi    //////////
	//generate all the pi, gi terms
	CLA4PGO(a=a[0..3], b=b0b3,
			p=P0b3, p[0]=p0, p[1]=p1, p[2]=p2, p[3]=p3,
			g=G0b3, g[0]=g0, g[1]=g1, g[2]=g2, g[3]=g3);
	CLA4PGO(a=a[4..7], b=b4b7,
			p=P4b7, p[0]=p4, p[1]=p5, p[2]=p6, p[3]=p7,
			g=G4b7, g[0]=g4, g[1]=g5, g[2]=g6, g[3]=g7);

	//////////    t = 2tau: PiI    //////////
	//p0i, p1i, p2i, p3i
	And4(a=p0,  b=p1,  c=p2,  d=p3,  out=p0i);
	And4(a=p4,  b=p5,  c=p6,  d=p7,  out=p1i);

	//////////    t = 3tau: GiI    //////////
	//g0i
	And(a=p3,  b=g2, out=p3g2);
	And3(a=p3, b=p2, c=g1, out=p3p2g1);
	And4(a=p3, b=p2, c=p1, d=g0, out=p3p2p1g0);
	Or4(a=p3p2p1g0, b=p3p2g1, c=p3g2, d=g3, out=g0i);

	//g1i
	And(a=p7,  b=g6, out=p7g6);
	And3(a=p7, b=p6, c=g5, out=p7p6g5);
	And4(a=p7, b=p6, c=p5, d=g4, out=p7p6p5g4);
	Or4(a=p7p6p5g4, b=p7p6g5, c=p7g6, d=g7, out=g1i);

	//////////    t = 5tau: C4i    //////////
	//c4
	AndOr(a0=p0i, a1=AS, b=g0i, out=c4);

	//c8
	And(a=p1i,  b=g0i, out=p1ig0i);
	And3(a=p1i, b=p0i, c=AS, out=p1ip0ic0);
	Or3(a=p1ip0ic0, b=p1ig0i, c=g1i, out=c8);

	//////////    t = 7tau: C(4i+k), Si    //////////
	CLA4PGI(p=P0b3,	  g=G0b3, us=us,   ci=AS,  s=s[0..3],   co=discard0, of=discard4);
	CLA4PGI(p=P4b7,   g=G4b7, us=us,   ci=c4,  s=s[4..7],   co=discard1, of=of);
}