CHIP MPLY8CSAu  {
    IN  a[8], b[8];
    OUT p[8], of;

    PARTS:
    And8b1(a=a, b=b[0], out=pp0);
    And8b1(a=a, b=b[1], out=pp1);
    And8b1(a=a, b=b[2], out=pp2);
    And8b1(a=a, b=b[3], out=pp3);
    And8b1(a=a, b=b[4], out=pp4);
    And8b1(a=a, b=b[5], out=pp5);
    And8b1(a=a, b=b[6], out=pp6);
    And8b1(a=a, b=b[7], out=pp7);

    //level_0: (pp0 pp1 pp2) (pp3 pp4 pp5) pp6 pp7 -> s00 c00 s01 c01 pp6 pp7
    CSA16u(a[0..7]=pp0,  b[1..8]=pp1,  ci[2..9]=pp2,  s=s00, co[0..14]=c00);
    CSA16u(a[3..10]=pp3, b[4..11]=pp4, ci[5..12]=pp5, s=s01, co[0..14]=c01);

    //level_1: (s00 c00 s01) (c01 pp6 pp7) -> s10 c10 s11 c11
    CSA16u(a=s00,	      b[1..15]=c00,	ci=s01,			s=s10,	co[0..14]=c10);
    CSA16u(a[1..15]=c01,  b[6..13]=pp6,	ci[7..14]=pp7,	s=s11,	co[0..14]=c11);

    //level_2: (s10 c10 s11) c11 -> s20 c20 c11
    CSA16u(a=s10, b[1..15]=c10, ci=s11, s=s20, co[0..14]=c20);

    //level_3: (s20 c20 c11) -> s30 c30
    CSA16u(a=s20, b[1..15]=c20, ci[1..15]=c11, s=s30, co[0..14]=c30);

    //CLA16Adder:
    CLA164Ha(a=s30, b[1..15]=c30, AS=false,
    		 s[0..7] = p,
    		 s[8] 	 = p8,
    		 s[9] 	 = p9,
    		 s[10]	 = p10,
    		 s[11]	 = p11,
    		 s[12]	 = p12,
    		 s[13]	 = p13,
    		 s[14]	 = p14,
    		 s[15]	 = p15);

    //Overflow:
    Or8t1(in[0]=p8, in[1]=p9, in[2]=p10,
    	  in[3]=p11, in[4]=p12, in[5]=p13,
    	  in[6]=p14, in[7]=p15, out=of);
}