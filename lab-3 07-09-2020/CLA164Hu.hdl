CHIP CLA164Hu  {
	//unsigned

	//ci: carrb-in, co: carrb-out, of: overflow
	IN		a[16], b[16], ci;
	OUT		s[16], co, of;

	PARTS:
	//p[0-9]+ : Pi
	//P([0-9]+)b([0-9]+) : P[\1..\2]

	//////////    t = 1tau: Pi, Gi    //////////
	//generate all the pi, gi terms
	CLA4PGO(a=a[0..3], b=b[0..3],
			p=P0b3, p[0]=p0, p[1]=p1, p[2]=p2, p[3]=p3,
			g=G0b3, g[0]=g0, g[1]=g1, g[2]=g2, g[3]=g3);
	CLA4PGO(a=a[4..7], b=b[4..7],
			p=P4b7, p[0]=p4, p[1]=p5, p[2]=p6, p[3]=p7,
			g=G4b7, g[0]=g4, g[1]=g5, g[2]=g6, g[3]=g7);
	CLA4PGO(a=a[8..11], b=b[8..11],
			p=P8b11, p[0]=p8, p[1]=p9, p[2]=p10, p[3]=p11,
			g=G8b11, g[0]=g8, g[1]=g9, g[2]=g10, g[3]=g11);
	CLA4PGO(a=a[12..15], b=b[12..15],
			p=P12b15, p[0]=p12, p[1]=p13, p[2]=p14, p[3]=p15,
			g=G12b15, g[0]=g12, g[1]=g13, g[2]=g14, g[3]=g15);

	//////////    t = 2tau: PiI    //////////
	//p0i, p1i, p2i, p3i
	And4(a=p0,  b=p1,  c=p2,  d=p3,  out=p0i);
	And4(a=p4,  b=p5,  c=p6,  d=p7,  out=p1i);
	And4(a=p8,  b=p9,  c=p10, d=p11, out=p2i);
	And4(a=p12, b=p13, c=p14, d=p15, out=p3i);

	//////////    t = 3tau: GiI    //////////
	//g0i
	And(a=p3,  b=g2, out=p3g2);
	And3(a=p3, b=p2, c=g1, out=p3p2g1);
	And4(a=p3, b=p2, c=p1, d=g0, out=p3p2p1g0);
	Or4(a=p3p2p1g0, b=p3p2g1, c=p3g2, d=g3, out=g0i);

	//g1i
	And(a=p7,  b=g6, out=p7g6);
	And3(a=p7, b=p6, c=g5, out=p7p6g5);
	And4(a=p7, b=p6, c=p5, d=g4, out=p7p6p5g4);
	Or4(a=p7p6p5g4, b=p7p6g5, c=p7g6, d=g7, out=g1i);

	//g2i
	And(a=p11,  b=g10, out=p11g10);
	And3(a=p11, b=p10, c=g9, out=p11p10g9);
	And4(a=p11, b=p10, c=p9, d=g8, out=p11p10p9g8);
	Or4(a=p11p10p9g8, b=p11p10g9, c=p11g10, d=g11, out=g2i);

	//g3i
	And(a=p15,  b=g14, out=p15g14);
	And3(a=p15, b=p14, c=g13, out=p15p14g13);
	And4(a=p15, b=p14, c=p13, d=g12, out=p15p14p13g12);
	Or4(a=p15p14p13g12, b=p15p14g13, c=p15g14, d=g15, out=g3i);

	//////////    t = 5tau: C4i    //////////
	//c4
	AndOr(a0=p0i, a1=ci, b=g0i, out=c4);

	//c8
	And(a=p1i,  b=g0i, out=p1ig0i);
	And3(a=p1i, b=p0i, c=ci, out=p1ip0ic0);
	Or3(a=p1ip0ic0, b=p1ig0i, c=g1i, out=c8);

	//c12
	And(a=p2i,  b=g1i, out=p2ig1i);
	And3(a=p2i, b=p1i, c=g0i, out=p2ip1ig0i);
	And4(a=p2i, b=p1i, c=p0i, d=ci, out=p2ip1ip0ic0);
	Or4(a=p2ip1ip0ic0, b=p2ip1ig0i, c=p2ig1i, d=g2i, out=c12);

	//c16 (unnecessarb, but will calculate for completeness)
	And(a=p3i,  b=g2i, out=p3ig2i);
	And3(a=p3i, b=p2i, c=g1i, out=p3ip2ig1i);
	And4(a=p3i, b=p2i, c=p1i, d=g0i, out=p3ip2ip1ig0i);
	And5(a=p3i, b=p2i, c=p1i, d=p0i, e=ci, out=p3ip2ip1ip0ic0);
	Or5(a=p3ip2ip1ip0ic0, b=p3ip2ip1ig0i, c=p3ip2ig1i, d=p3ig2i, e=g3i, out=c16);

	//////////    t = 7tau: C(4i+k), Si    //////////
	CLA4PGIu(p=P0b3,   g=G0b3,   ci=ci,  s=s[0..3],   co=discard0, of=discard4);
	CLA4PGIu(p=P4b7,   g=G4b7,   ci=c4,  s=s[4..7],   co=discard1, of=discard5);
	CLA4PGIu(p=P8b11,  g=G8b11,  ci=c8,  s=s[8..11],  co=discard2, of=discard6);
	CLA4PGIu(p=P12b15, g=G12b15, ci=c12, s=s[12..15], co=discard3, of=of);
	//the (^) onlb difference between signed and unsigned variants lies here
}